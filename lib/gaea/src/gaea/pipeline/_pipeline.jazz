;;;==============
;;;  GaeaScheme
;;;==============
;;;
;;;; Pipeline
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2021
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module gaea.pipeline jazz


(import (jazz.platform)
        (gaea.array)
        (gaea.buffer)
        (gaea.engine)
        (gaea.metal)
        (gaea.opengl.glew)
        (gaea.program))


;;;
;;;; Pipeline
;;;


(class Pipeline extends Object
  
  
  (slot attributes getter generate)
  (slot layouts    getter generate)
  (slot pipeline   getter generate)
  (slot program    getter generate)
  
  
  (method override (initialize self sources attributes layouts)
    (nextmethod self)
    (set! self.attributes attributes)
    (set! self.layouts layouts)
    (bind (metal opengl) sources
      (if (metal?)
          (bind (source vs fs) metal
            (let ((lib (metalMakeLibrary source)))
              (let ((desc (metalMakeVertexDescriptor)))
                (let ((index 0))
                  (for-each (lambda (attribute)
                              (bind (name format offset buffer-index) attribute
                                (let ((format (case format
                                                ((float2) MTLVertexFormatFloat2)
                                                ((float4) MTLVertexFormatFloat4))))
                                  (metalVertexDescriptorAttribute desc index format offset buffer-index)))
                              (increase! index))
                            attributes))
                (for-each (lambda (layout)
                            (bind (buffer-index stride) layout
                              (metalVertexDescriptorLayout desc buffer-index MTLVertexStepFunctionPerVertex stride)))
                          layouts)
                (let ((pipeline (metalMakeRenderPipelineState lib vs fs desc)))
                  (set! self.pipeline pipeline)))))
        (bind (vs fs) opengl
          (let ((program (new Program)))
            (set-vs program vs)
            (set-fs program fs)
            (link program)
            (set! self.program program))))))
  
  
  (method public (with-pipeline self encoder thunk)
    (if (metal?)
        (begin
          (metalEncoderSetRenderPipelineState encoder pipeline)
          (thunk))
      (use program)
      (thunk)
      (unuse program)))
  
  
  (method public (setup-array self array)
    (when (opengl?)
      (glBindVertexArray (get-array array))
      (for-each (lambda (layout)
                  (bind (buffer-index stride) layout
                    ;; - 1 is a quick hack for tests
                    (let ((buffer (element (get-buffers array) (- buffer-index 1))))
                      (bind-buffer buffer)
                      (let ((offset 0))
                        (for-each (lambda (attribute)
                                    (bind (name format offset buffer-index) attribute
                                      (let ((location (get-attribute program name))
                                            (size (case format
                                                    ((float2) (* 2 float-size))
                                                    ((float3) (* 3 float-size))
                                                    ((float4) (* 4 float-size))))
                                            (type GL_FLOAT))
                                        (enable-vertex-attrib buffer location)
                                        (set-attrib-pointer buffer location size type stride offset)
                                        (increase! offset size))))
                                  attributes))
                      (unbind-buffer buffer))))
                layouts)))
  
  
  (method public (bind-uniforms self . rest)
    (when (opengl?)
      (apply bind-uniforms program rest)))
  
  
  (method public (create-array self mesh)
    (when (opengl?)
      (create-array program mesh)))
  
  
  (method public (unbind-uniforms self)
    (when (opengl?)
      (unbind-uniforms program))))


;;;
;;;; Pipelines
;;;


(definition protected pipelines
  (make-table test: eq?))


(definition public (register-pipeline name pipeline)
  (table-set! pipelines name pipeline))

(definition public (registered-pipeline name)
  (let ((pipeline (table-ref pipelines name #f)))
    (if (procedure? pipeline)
        (let ((pipeline (pipeline)))
          (table-set! pipelines name pipeline)
          pipeline)
      pipeline)))

(definition public (require-pipeline name)
  (or (registered-pipeline name)
      (error "Unknown pipeline: {s}" name))))
