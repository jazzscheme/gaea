;;;==============
;;;  GaeaScheme
;;;==============
;;;
;;;; Program
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2021
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module gaea.program jazz


(import (gaea.engine)
        (gaea.metal)
        (gaea.opengl)
        (gaea.opengl.glew)
        (gaea.shader))


;;;
;;;; Program
;;;


(class Program extends Object
  
  
  (slot name            <object> getter generate)
  (slot vertex-shader   <object> accessors generate)
  (slot fragment-shader <object> accessors generate)
  (slot library         <object> getter generate)
  (slot program         <object> getter generate)
  (slot inputs          <object> getter generate)
  (slot attributes      <object> getter generate)
  (slot uniforms        <object> getter generate)
  
  
  (method override (initialize self sources . inputs)
    (nextmethod self)
    (set! self.inputs inputs)
    (bind (metal opengl) sources
      (if (metal?)
          (bind (source vs fs) metal
            (set! library (metalMakeLibrary source))
            (set! vertex-shader vs)
            (set! fragment-shader fs))
        (bind (vs fs) opengl
          (set! vertex-shader (new Shader GL_VERTEX_SHADER))
          (set! fragment-shader (new Shader GL_FRAGMENT_SHADER))
          (set! program (glCreateProgram))
          (set-code vertex-shader vs)
          (set-code fragment-shader fs)
          (link self)))))
  
  
  (method package virtual (prepare self)
    )
  
  
  (method protected virtual (create-vertex-shader self)
    )
  
  (method protected virtual (create-fragment-shader self)
    )
  
  
  (method public (set-vs self code)
    (set-code vertex-shader code))
  
  (method public (set-fs self code)
    (set-code fragment-shader code))
  
  
  (method public (link self)
    (create-shaders self)
    (attach-shaders self))
  
  
  (method public (relink self)
    (detach-shaders self)
    (set-vs self (create-vertex-shader self))
    (set-fs self (create-fragment-shader self))
    (prepare self))
  
  
  (method public (create-shaders self)
    (create-shader vertex-shader)
    (create-shader fragment-shader)
    (compile-shader vertex-shader)
    (compile-shader fragment-shader))
  
  
  (method public (attach-shaders self)
    (when (and (get-shaderid vertex-shader)
               (get-shaderid fragment-shader))
      (glAttachShader program (get-shaderid vertex-shader))
      (glAttachShader program (get-shaderid fragment-shader))
      (glLinkProgram program)
      (when (/= (glGetProgramiv* program GL_LINK_STATUS) GL_TRUE)
        (let ((loglength (glGetProgramiv* program GL_INFO_LOG_LENGTH)))
          (when (> loglength 0)
            (error "{a}" (glGetProgramInfoLog* program loglength)))))))
  
  
  (method public (detach-shaders self)
    (when (and (get-shaderid vertex-shader)
               (get-shaderid fragment-shader))
      (glDetachShader program (get-shaderid vertex-shader))
      (glDetachShader program (get-shaderid fragment-shader))
      (set-shaderid vertex-shader #f)
      (set-shaderid fragment-shader #f)))
  
  
  (method protected virtual (setup-transform-feedback self)
    )
  
  
  (method protected virtual (enable-attributes self mesh)
    )
  
  
  (method public (create-array self mesh)
    (let ((array (glGenVertexArrays*)))
      (glBindVertexArray array)
      (enable-attributes self mesh)
      (glBindVertexArray 0)
      array))
  
  
  (method public (use self)
    (glUseProgram program))
  
  
  (method public (unuse self)
    (glUseProgram 0))
  
  
  ;;;
  ;;;; Uniforms
  ;;;
  
  
  (method public (get-uniform self name)
    (glGetUniformLocation* program name))
  
  
  (method public (add-uniform self name)
    (let ((location (glGetUniformLocation* program name)))
      (table-set! uniforms name location)
      location))
  
  
  ;;;
  ;;;; Attributes
  ;;;
  
  
  (method public (get-attribute self name)
    (glGetAttribLocation* program name))
  
  
  (method public (add-attribute self name)
    (let ((location (glGetAttribLocation* program name)))
      (table-set! attributes name location)
      location)))


;;;
;;;; Programs
;;;


(definition protected programs
  (make-table test: eq?))


(definition public (register-program name program)
  (table-set! programs name program))

(definition public (registered-program name)
  (let ((program (table-ref programs name #f)))
    (if (procedure? program)
        (let ((program (program)))
          (table-set! programs name program)
          program)
      program)))

(definition public (require-program name)
  (or (registered-program name)
      (error "Unknown program: {s}" name))))
