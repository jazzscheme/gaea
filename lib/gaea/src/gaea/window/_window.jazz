;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Gaea Windows
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module gaea.window jazz


(export (gaea.window.platform.cocoa   (cond cocoa))
        (gaea.window.platform.windows (cond windows))
        (gaea.window.platform.x11     (cond x11)))

(import (jazz.component)
        (jazz.math)
        (jazz.platform)
        (jazz.screen)
        (jazz.window)
        (gaea.engine)
        (gaea.opengl.glew)
        (gaea.opengl.render)
        (gaea.window.platform.cocoa   (cond cocoa))
        (gaea.window.platform.windows (cond windows))
        (gaea.window.platform.x11     (cond x11)))


;;;
;;;; Window
;;;


(class Gaea-Window extends Overlapped


  (slot device             initialize #f accessors generate)
  (slot context            initialize #f accessors generate)
  (slot context-mutex      initialize #f getter generate)
  (slot previous-placement initialize #f getter generate)
  (slot window-mode        initialize #f getter generate)
  (slot render-thread      initialize #f getter generate)

  
  (method override (opengl-window? self)
    (opengl?))


  (method override (prepare self rest)
    (nextmethod self rest)
    (set! context-mutex (make-mutex 'context))
    (set! window-mode (if (request-fullscreen?) 'fullscreen 'windowed))
    (prepare-engine-window self))
  
  
  (method public (with-context-current self thunk)
    (with-reentrant-render-mutex
      (lambda ()
        (unwind-protect
            (begin
              (mutex-lock! context-mutex)
              (when (opengl?)
                (make-context-current self))
              (thunk)) 
          (mutex-unlock! context-mutex)))))

  
  (method override (size-change self state size)
    (nextmethod self state size)
    (when (and (opened? self) (opengl-setup? self))
      (with-context-current self
        (lambda ()
          (let ((width (get-width size))
                (height (get-height size)))
            (resize-scene self (cast <fl> width) (cast <fl> height)))))))

  
  (method package virtual (resize-scene self width <fl> height <fl>)
    (when (opengl?)
      (gaea.engine.opengl:opengl-resize-scene self width height)))

  
  (method package virtual (setup-engine self)
    (when (opengl?)
      (gaea.engine.opengl:opengl-setup)))


  ;;;
  ;;;; Fullscreen
  ;;;


  ;; quicky
  (cond-expand
    (cocoa
     (method public (toggle-windowed-mode self)
       (platform-set-fullscreen-windowed-mode handle)
       ;; quicky
       (set! window-mode (if (eq? window-mode 'windowed)
                             'fullscreen
                           'windowed))))
    (else
     (method public (toggle-windowed-mode self)
       (case window-mode
         ((fullscreen)
          (restore-from-fullscreen self))
         ((windowed)
          (set-fullscreen-windowed-mode self))
         (else
          (restore-windowed-mode self))))))


  (method public (set-fullscreen-windowed-mode self)
    (set! previous-placement (get-placement self))
    (platform-set-fullscreen-windowed-mode handle)
    (set! window-mode 'fullscreen-windowed))


  (method public (restore-from-fullscreen self)
    (platform-restore-from-fullscreen handle)
    (set! window-mode 'windowed))


  (method public (restore-windowed-mode self)
    (when previous-placement
      (set-window-placement handle previous-placement))
    (platform-restore-windowed-mode handle)
    (set! window-mode 'windowed))


  (method public (set-fullscreen-mode self)
    (platform-set-fullscreen-mode handle))


  ;;;
  ;;;; Render
  ;;;
  
  
  (method (start-render self)
    (let ((thread
            (new-thread
              (lambda ()
                (declare (proper-tail-calls))
                (let ((period (/ 1. 30.)))
                  (let (loop)
                    (render-scene self)
                    (sleep period)
                    (loop))))
              'render)))
      (set! render-thread thread)
      (thread-start! thread)))
  
  
  (method (stop-render self)
    (when render-thread
      (thread-terminate! render-thread)
      (set! render-thread #f)))

  
  (method override (paint self)
    (with-painting self
      (lambda ()
        (with-context-current self
          (lambda ()
            (render-scene self))))))

  
  (method protected virtual (render-ready? self)
    #t)

  
  (method protected virtual (render-scene self)
    (when (render-ready? self)
      (clear-scene self)
      (draw-scene self)
      (render-sync self)))
  
  
  (method protected virtual (clear-scene self)
    (when (opengl?)
      (glClearColor 0.0 0.0 0.0 0.0)
      (glClear (bitwise-ior GL_COLOR_BUFFER_BIT GL_DEPTH_BUFFER_BIT))))

  
  (method protected virtual (draw-scene self)
    (when (opengl?)
      (gaea.engine.opengl:opengl-draw-scene)))

  
  (method public (render-sync self)
    (with-reentrant-render-mutex
      (lambda ()
        (swap-buffers handle device))))


  (method override (scroll-window self delta rect clip-rect)
    (assert handle)
    (invalidate-rect self clip-rect))))
